defmodule StringNaming.H do
  def nested_module(mod, children) do
    [funs, mods] =
      Enum.reduce(children, [%{}, %{}], fn
        {k, v}, [funs, mods] when is_binary(v) -> [Map.put(funs, k, v), mods]
        {k, v}, [funs, mods] -> [funs, Map.put(mods, k, v)]
      end)

    ast =
      for {name, value} <- funs do
        name =
          name |> String.replace(~r/\A(\d)/, "N_\\1") |> Macro.underscore() |> String.to_atom()

        quote do: def(unquote(name)(), do: <<String.to_integer(unquote(value), 16)::utf8>>)
      end ++
        [
          quote do
            def __all__ do
              :functions
              |> __MODULE__.__info__()
              |> Enum.map(fn
                {:__all__, 0} -> nil
                {k, 0} -> {k, apply(__MODULE__, k, [])}
                _ -> nil
              end)
              |> Enum.filter(& &1)
            end
          end
        ]

    Module.create(Module.concat(mod), ast, Macro.Env.location(__ENV__))
    StringNaming.H.nesteds(mod, mods)
  end

  def nesteds(nested \\ [], map_or_code)

  def nesteds(nested, %{} = map) do
    Enum.each(map, fn
      {_key, code} when is_binary(code) ->
        :ok

      {k, v} ->
        mod = :lists.reverse([k | :lists.reverse(nested)])
        StringNaming.H.nested_module(mod, v)
    end)
  end
end

defmodule StringNaming do
  @moduledoc ~S"""
  The sibling of [`String.Casing`](https://github.com/elixir-lang/elixir/blob/9873e4239f063e044e5d6602e173ebee4f32391d/lib/elixir/unicode/properties.ex#L57),
    `String.Break` and `String.Normalizer` from Elixir core.

  It parses the [`NamesList.txt`](http://www.unicode.org/Public/UCD/latest/ucd/NamesList.txt) file provided by Consortium, building
    the set of nested modules under `StringNaming`. Each nested module is granted with `__all__/0` function that returns all the
    available symbols in that particular namespace, as well as with methods returning a symbol by it‚Äôs name.

  ## Examples

      iex> StringNaming.AnimalSymbols.monkey
      "üêí"
      iex> StringNaming.FrakturSymbols.Mathematical.Fraktur.Capital.__all__
      [a: "ùîÑ", b: "ùîÖ", d: "ùîá", e: "ùîà", f: "ùîâ", g: "ùîä", j: "ùîç",
       k: "ùîé", l: "ùîè", m: "ùîê", n: "ùîë", o: "ùîí", p: "ùîì", q: "ùîî",
       s: "ùîñ", t: "ùîó", u: "ùîò", v: "ùîô", w: "ùîö", x: "ùîõ", y: "ùîú"]

  """

  @categories Enum.uniq(
                StringNaming.Defaults.categories() ++
                  Application.compile_env(:string_naming, :categories, [])
              )

  data_path = Path.join([__DIR__, "string_naming", "unicode", "NamesList.txt"])

  ~S"""
  0021	EXCLAMATION MARK
  	= factorial
  	= bang
  	x (inverted exclamation mark - 00A1)
  	x (latin letter retroflex click - 01C3)
  """

  extract_prop = fn
    _rest, [_category, _names, _props] = acc ->
      # TODO make properties available as well
      # IO.inspect rest, label: "‚òÖ property"
      acc
  end

  underscore = fn name ->
    name
    |> String.trim()
    |> String.replace(~r/\A(\d)/, "N_\\1")
    |> String.replace(~r/[^A-Za-z\d_ ]/, " ")
    |> String.split(" ")
    |> Enum.filter(&(&1 != ""))
    |> Enum.join("_")
    |> Macro.underscore()
  end

  @selected @categories
            |> Enum.filter(fn
              <<"#", _::binary>> -> false
              <<"=", _::binary>> -> false
              <<"+", _::binary>> -> false
              _ -> true
            end)
            |> Enum.map(&underscore.(&1))
  extract_name = fn
    _, <<"<"::binary, _::binary>>, [_category, _names, _props] = acc ->
      acc

    code, name, [category, names, props] ->
      [category, [{code, underscore.(name), category} | names], props]
  end

  [_category, names, _props] =
    Enum.reduce(File.stream!(data_path), ["Unknown", [], %{}], fn
      <<";"::binary, _::binary>>, acc ->
        acc

      <<"@\t"::binary, category::binary>>, [_, names, props] ->
        category = underscore.(category)
        category = if Enum.member?(@selected, category), do: category, else: ""
        [category, names, props]

      <<"@"::binary, _::binary>>, acc ->
        acc

      <<"\t"::binary, rest::binary>>, acc ->
        extract_prop.(rest, acc)

      code_name, [category, _, _] = acc when category != "" ->
        with [code, name] <- :binary.split(code_name, "\t") do
          extract_name.(code, name, acc)
        end

      <<"00", _::binary-size(2), "\t", _::binary>> = code_name, [_, names, props] ->
        with [code, name] <- :binary.split(code_name, "\t") do
          extract_name.(code, name, ["ascii", names, props])
        end

      _, acc ->
        acc
    end)

  names_tree =
    Enum.reduce(names, %{}, fn {code, name, category}, acc ->
      modules = [category | String.split(name, "_")] |> Enum.map(&Macro.camelize/1)

      {acc, ^modules} =
        Enum.reduce(modules, {acc, []}, fn
          key, {acc, keys} ->
            keys = :lists.reverse([key | :lists.reverse(keys)])

            {_, result} =
              get_and_update_in(acc, keys, fn
                nil -> {nil, %{}}
                map when is_map(map) -> {map, map}
                other -> {other, %{}}
              end)

            {result, keys}
        end)

      put_in(acc, modules, code)
    end)

  StringNaming.H.nesteds(["StringNaming"], names_tree)

  @doc ~S"""
  Returns graphemes for modules that have names matching the regular expression given as a parameter.
  The response is a plain keyword list with names taken from concatenated nested module names.

  ## Examples

      iex> StringNaming.graphemes ~r/AnimalFace/
      [
        animalfaces_bear_face: "üêª",
        animalfaces_cat_face: "üê±",
        animalfaces_cow_face: "üêÆ",
        animalfaces_dog_face: "üê∂",
        animalfaces_dragon_face: "üê≤",
        animalfaces_frog_face: "üê∏",
        animalfaces_hamster_face: "üêπ",
        animalfaces_horse_face: "üê¥",
        animalfaces_monkey_face: "üêµ",
        animalfaces_mouse_face: "üê≠",
        animalfaces_panda_face: "üêº",
        animalfaces_pig_face: "üê∑",
        animalfaces_pig_nose: "üêΩ",
        animalfaces_rabbit_face: "üê∞",
        animalfaces_spouting_whale: "üê≥",
        animalfaces_tiger_face: "üêØ",
        animalfaces_wolf_face: "üê∫"
      ]

      iex> StringNaming.graphemes ~r/fraktur.small/i
      [
        fraktursymbols_mathematical_fraktur_small_a: "ùîû",
        fraktursymbols_mathematical_fraktur_small_b: "ùîü",
        fraktursymbols_mathematical_fraktur_small_c: "ùî†",
        fraktursymbols_mathematical_fraktur_small_d: "ùî°",
        fraktursymbols_mathematical_fraktur_small_e: "ùî¢",
        fraktursymbols_mathematical_fraktur_small_f: "ùî£",
        fraktursymbols_mathematical_fraktur_small_g: "ùî§",
        fraktursymbols_mathematical_fraktur_small_h: "ùî•",
        fraktursymbols_mathematical_fraktur_small_i: "ùî¶",
        fraktursymbols_mathematical_fraktur_small_j: "ùîß",
        fraktursymbols_mathematical_fraktur_small_k: "ùî®",
        fraktursymbols_mathematical_fraktur_small_l: "ùî©",
        fraktursymbols_mathematical_fraktur_small_m: "ùî™",
        fraktursymbols_mathematical_fraktur_small_n: "ùî´",
        fraktursymbols_mathematical_fraktur_small_o: "ùî¨",
        fraktursymbols_mathematical_fraktur_small_p: "ùî≠",
        fraktursymbols_mathematical_fraktur_small_q: "ùîÆ",
        fraktursymbols_mathematical_fraktur_small_r: "ùîØ",
        fraktursymbols_mathematical_fraktur_small_s: "ùî∞",
        fraktursymbols_mathematical_fraktur_small_t: "ùî±",
        fraktursymbols_mathematical_fraktur_small_u: "ùî≤",
        fraktursymbols_mathematical_fraktur_small_v: "ùî≥",
        fraktursymbols_mathematical_fraktur_small_w: "ùî¥",
        fraktursymbols_mathematical_fraktur_small_x: "ùîµ",
        fraktursymbols_mathematical_fraktur_small_y: "ùî∂",
        fraktursymbols_mathematical_fraktur_small_z: "ùî∑"
      ]

      iex> StringNaming.graphemes ~r/\Aspace/i, false
      [
        space_medium_mathematical_space: "‚Åü",
        space_narrow_no_break_space: "‚ÄØ",
        space_ogham_space_mark: "·öÄ",
        spaces_em_quad: "‚ÄÅ",
        spaces_em_space: "‚ÄÉ",
        spaces_en_quad: "‚ÄÄ",
        spaces_en_space: "‚ÄÇ",
        spaces_figure_space: "‚Äá",
        spaces_four_per_em_space: "‚ÄÖ",
        spaces_hair_space: "‚Ää",
        spaces_punctuation_space: "‚Äà",
        spaces_six_per_em_space: "‚ÄÜ",
        spaces_thin_space: "‚Äâ",
        spaces_three_per_em_space: "‚ÄÑ"
      ]


  """
  def graphemes(%Regex{} = filter, modules_only? \\ true) do
    with {:ok, modules} <- :application.get_key(:string_naming, :modules) do
      modules
      |> Enum.filter(fn m ->
        case {modules_only?, to_string(m)} do
          {false, _} ->
            match?({:module, ^m}, Code.ensure_loaded(m)) and function_exported?(m, :__all__, 0)

          {_, <<"Elixir.StringNaming."::binary, name::binary>>} ->
            Regex.match?(filter, name)

          _ ->
            false
        end
      end)
      |> Enum.flat_map(fn m ->
        m
        |> apply(:__all__, [])
        |> Enum.reduce([], fn {k, v}, acc ->
          <<"Elixir.StringNaming."::binary, name::binary>> = to_string(m)

          name =
            name
            |> String.split(~r/\W/)
            |> Kernel.++([k])
            |> Enum.join("_")

          if Regex.match?(filter, name),
            do: [{name |> String.downcase() |> String.to_atom(), v} | acc],
            else: acc
        end)
        |> Enum.reverse()
      end)
    end
  end
end

:code.purge(StringNaming.H)
:code.delete(StringNaming.H)
